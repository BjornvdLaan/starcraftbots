use NexusBot as knowledge.
use "../GenericMods/GenericPredicates" as knowledge.
use "../GenericMods/GenericPercepts" as module.

module NexusBotEvent {
	%%%%%%%%%%%%%%% --------------------------------------- Percepts --------------------------------------- %%%%%%%%%%%%%%%
	
	% Get generic percepts
	if true then GenericPercepts.
	
	% Update knowledge about capacity
	if percept(resources(_, _, C, T)), bel(capacity(OC, OT)) then delete(capacity(OC, OT)) + insert(capacity(C, T)).
	
	% Update knowledge about the queue
	if bel(queueSize(No)), percept(queueSize(N)) then delete(queueSize(No)) + insert(queueSize(N)).
	
	% Update status
	if percept(status(Health, Shield, Energy, Cond, X, Y)), bel(status(OHealth, OShield, OEnergy, OCond, OX, OY))
	then delete(status(OHealth, OShield, OEnergy, OCond, OX, OY)) + insert(status(Health, Shield, Energy, Cond, X, Y)).
	
	%%%%%%%%%%%%%%% --------------------------------------- GOALS --------------------------------------- %%%%%%%%%%%%%%%
	
	%% If there is an Assimilator
	% If there is no goal about making units and there are less than (1.5 * amount of mineral fields + 3) probes, then build more probes
	% (3 extra probes are for vespene mining)
	if not(goal(unitAmount(_,_))), bel(unitAmount("Protoss Assimilator", AssiAmount), AssiAmount > 0, N = 8, unitAmount("Protoss Probe",Y), (Y < (1.5 * N + 3))) then adopt(unitAmount("Protoss Probe", N * 1.5 + 3)).
	
	%% If there is no Assimilator
	% If there is no goal about making units and there are less than (1.5 * amount of mineral fields) probes, then build more probes
	if not(goal(unitAmount(_,_))), bel(not(unitAmount("Protoss Assimilator", AssiAmount)), N = 8, unitAmount("Protoss Probe",Y), (Y < (1.5 * N))) then adopt(unitAmount("Protoss Probe", N * 1.5)).
	
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%
	
	% If 12 Zealots or 5 Dragoons are built, give the attack order
	if bel(unitAmount("Protoss Zealot", N), N > 11) then insert(attackOrder) + allother.send(attackOrder).
	%if bel(unitAmount("Protoss Dragoon", N), N > 5) then insert(attackOrder) + (dragoonBot).send(attackOrder).
	
	% If someone sends a new mineral field, add it to knowledge base
	if (_).sent(mineralField(Id)), not(bel(mineralField(Id))) then insert(mineralField(Id)).
	
	% If someone asks the nexus location, send it
	forall (Name).sent(ownBase), bel(status(_, _, _, _, X, Y)) do (Name).send(ownBase(X, Y)).
	
	% If a new probe is created, insert it in knowledge base
	forall (Name).sent(probe(Id)) do insert(probe(Name, Id)).
		
}

