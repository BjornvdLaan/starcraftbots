use ProbeBot as knowledge.
use "../GenericMods/GenericPredicates" as knowledge.
use "../GenericMods/GenericPercepts" as module.

module ProbeBotEvent {
	%%%%%%%%%%%%%%% --------------------------------------- Percepts --------------------------------------- %%%%%%%%%%%%%%%
	
	% get GenericPercepts
	if true then GenericPercepts.
	
	% Current state of the probe
	if percept(status(_,_,_,NewCond,_,_)), bel(state(OldCond), NewCond \= OldCond) then delete(state(OldCond)) + insert(state(NewCond)).
	
	% Locations of Mineral fields
	forall percept(mineralField(Id,Amount,_,X,Y)), not(bel(mineralLoc(Id,Amount,X,Y))) do insert(mineralLoc(Id,Amount,X,Y)).
	forall bel(mineralLoc(Id,Amount,X,Y)), not(percept(mineralField(Id,Amount,_,X,Y))) do delete(mineralLoc(Id,Amount,X,Y)).
	
	% Location of Vespene Geyser
	forall percept(vespeneGeyser(Id,R,Rg,Xv,Yv)), not(bel(vespeneLoc(Id,R,Rg,Xv,Yv))) do insert(vespeneLoc(Id,R,Rg,Xv,Yv)).
	forall bel(vespeneLoc(Id,R,Rg,Xv,Yv)),not(percept(vespeneGeyser(Id,R,Rg,Xv,Yv))) do delete(vespeneLoc(Id,R,Rg,Xv,Yv)).
	
	% Location of Construction Sites
	forall percept(constructionSite(X,Y,B)), not(bel(constructLoc(X,Y,B))) do insert(constructLoc(X,Y,B)).
	forall bel(constructLoc(X,Y,B)),not(percept(constructionSite(X,Y,B))) do delete(constructLoc(X,Y,B)).
	
	% The handling of the capacity percept
	if percept(resources(_, _, C, T)), bel(capacity(OldC, OldT), (not(OldC == C) ; not(OldT == T))) then delete(capacity(OldC, OldT)) + insert(capacity(C, T)).
	
	% The handling of the workerActivity
	forall percept(workerActivity(ID,ACT)), bel(not(workerActivity(ID,ACT))) do insert(workerActivity(ID,ACT)).
	forall bel(workerActivity(ID,ACT)),not(percept(workerActivity(ID,ACT))) do delete(workerActivity(ID,ACT)).
	
	
	%%%%%%%%%%%%%%% --------------------------------------- GOALS --------------------------------------- %%%%%%%%%%%%%%%
	
	% If there are Mineral patches, count the number
	%if bel( mineralLoc(Id,_,_,_)), percept(mineralField(Id,_,_,_,_)) then insert(mineralCount(X+1)).
	%if bel(mineralCount(OldCount)), aggregate_all(count, mineralLoc(Id), Count) then delete(mineralCount(OldCount)) + insert(mineralCount(Count)).
	
	% If there is no Pylon yet, create one
	if bel(builder, not(unitAmount("Protoss Pylon", PylAmount)), PylAmount = 1) then adopt(unitAmount("Protoss Pylon", 1)).
	
	% build one gateway
	if bel(builder, not(unitAmount("Protoss Gateway", _)), unitAmount("Protoss Pylon",_)) then adopt(unitAmount("Protoss Gateway", 1)).
	% build second gateway
	if bel(builder, unitAmount("Protoss Gateway", 1), unitAmount("Protoss Pylon",_)) then adopt(unitAmount("Protoss Gateway", 2)).
	%if bel(builder, not(unitAmount("Protoss Gateway", GateAmount)), unitAmount("Protoss Pylon", _)) then adopt(unitAmount("Protoss Gateway", 2)).
	
	% If there is not enough capacity, build more pylons
	if bel(builder, unitAmount("Protoss Pylon", PylAmount), PylAmount_Des is PylAmount+1, capacity(CS, TS), TS - CS < 8) then adopt(unitAmount("Protoss Pylon", PylAmount_Des)).
	
	% build cybernetics institute
	if bel(builder, not(unitAmount("Protos Cybernetics Core",1)), unitAmount("Protoss Pylon",_), unitAmount("Protoss Gateway", 1)) then adopt(unitAmount("Protoss Cybernetics Core", 1)).
	
	% build assimilator
	if bel(builder, not(unitAmount("Protoss Assimilator", _))) then adopt(unitAmount("Protoss Assimilator", 1)).
	
	% if role is vespene, collect vespene
	if bel(vespene, unitAmount("Protoss Assimilator", AssiAmount), AssiAmount > 0, self(Id, _)) then adopt(workerActivity(Id, gatheringGas)).
	
	% If the worker is not busy, start gathering minerals
	if bel(self(Id, _)) then adopt(workerActivity(Id, gatheringMinerals)).
	
	%if bel(workerActivity(Id,_), unitAmount("Protoss Pylon", PylAmount), PylAmount >= 1) then adopt(workerActivity(Id, gatheringMinerals)).
	
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%
	forall bel(mineralLoc(MinId,_,_,_)) do (nexusBot).send(mineralF(MinId)).
	
	if (_).sent(builder) then insert(builder).
	
	if (_).sent(vespene) then insert(vespene).
	
	
}

